plugins {
    id 'java'
    id 'application'
}

java {
    sourceCompatibility = 8
    targetCompatibility = 8

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

project.ext.lwjglVersion = "3.3.6"
project.ext.lwjgl3awtVersion = "0.1.8"
project.ext.jomlVersion = "1.10.5"

group = 'com.towergames'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-freetype"
    implementation "org.lwjgl:lwjgl-tinyfd"
    ['natives-windows-arm64', 'natives-windows', 'natives-windows-x86', 'natives-linux-arm64', 'natives-linux-arm32',
     'natives-linux', 'natives-macos-arm64', 'natives-macos'].each { natives ->
        implementation "org.lwjgl:lwjgl::${natives}"
        implementation "org.lwjgl:lwjgl-glfw::${natives}"
        implementation "org.lwjgl:lwjgl-opengl::${natives}"
        implementation "org.lwjgl:lwjgl-stb::${natives}"
        implementation "org.lwjgl:lwjgl-freetype::${natives}"
        implementation "org.lwjgl:lwjgl-tinyfd::${natives}"
    }

    implementation "de.javagl:jgltf-model:2.0.3"
    implementation "org.joml:joml:${jomlVersion}"
    implementation "ch.qos.logback:logback-classic:1.2.13"
}

application {
    mainClass = 'com.towergames.towerlib.demo.TowerLibDemo'
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        jvmArgs '-XstartOnFirstThread'
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.towergames.towerlib.demo.TowerLibDemo'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}